buildscript {
    ext.kotlin_version = '1.4.10'

    repositories {
        mavenCentral()
        maven {
            name 'JFrog OSS snapshot repo'
            url  'https://oss.jfrog.org/oss-snapshot-local/'
        }
        maven { url "https://repo.maven.apache.org/maven2" }
        maven { url 'https://plugins.gradle.org/m2/' }
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'application'
    id "org.jlleitschuh.gradle.ktlint" version "9.4.0"
    id 'maven-publish'
    id "com.jfrog.bintray" version "1.8.5"
}

group = 'com.strumenta.mpsinterop'
version = '0.1.1'

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    compile 'com.google.code.gson:gson:2.8.6'
    compile 'com.github.ajalt:clikt:2.1.0'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

application {
    mainClassName = 'com.strumenta.mps.exporter.ExportertoolKt'
}

jar {

}

ktlint {
    disabledRules = ["no-wildcard-imports"]
}


task fatJar(type: Jar) {
    manifest {
        attributes "Main-Class": 'com.strumenta.mps.exporter.ExportertoolKt'
    }
    archiveBaseName = 'mpsinterface-fatJar'
    archiveVersion = 'latest'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

def fatJarFile = file("$buildDir/libs/mpsinterface-fatJar-latest.jar")
def fatJarArtifact = artifacts.add('archives', fatJarFile) {
    type 'jar'
    builtBy 'fatJar'
}

publishing {
    publications {
        mpsInterface(MavenPublication) {
            groupId project.group
            version project.version

            from components.java
        }
        mpsInterfaceFatJar(MavenPublication) {
            groupId project.group
            artifactId 'mpsinterface-fatjar'
            version project.version

            artifact fatJarArtifact
        }
    }
}

bintray {
    user = rootProject.findProperty('bintray_user')
    key = rootProject.findProperty('bintray_api_key')
    pkg {
        repo = 'strumenta-oss-maven'
        name = project.name
        userOrg = 'strumenta'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/Strumenta/mpsinterop'
        version {
            name = 'v' + project.version
            desc = 'Version ' + project.version
            released = new Date()
            vcsTag = 'v' + project.version
        }
    }

    publications = ['mpsInterface', 'mpsInterfaceFatJar']
    dryRun = false
    publish = true
    override = true
}
